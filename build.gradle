buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0")
    }
}

group = 'bhavikk'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'com.palantir.docker'

jar {
    baseName = 'movie-service'
    version =  '1.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    compile.exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

// Runs coverage verification and report every test run
test { 
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

docker {
    name "${project.group}/${jar.baseName}:${jar.version}"
    tags 'latest'
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.9.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator', version: '1.5.9.RELEASE'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-consul-discovery', version: '1.3.0.RELEASE'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-consul-config', version: '1.3.0.RELEASE'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.2'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '1.5.9.RELEASE'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.12.Final'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '6.0.5.Final'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-vault-config', version: '1.1.0.RELEASE'

    runtime group: 'com.h2database', name: 'h2', version: '1.4.196'
    runtime group: 'org.postgresql', name: 'postgresql', version: '42.1.4'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.5.9.RELEASE'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.12.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}